plugins {
  id "com.diffplug.gradle.spotless" version "3.28.0"
  id "org.gradle.playframework" version "0.10"
  id "org.flywaydb.flyway" version "6.5.6"
  id "scala"
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    name "lightbend-maven-release"
    url "https://repo.lightbend.com/lightbend/maven-releases"
  }
  ivy {
    name "lightbend-ivy-release"
    url "https://repo.lightbend.com/lightbend/ivy-releases"
    layout "ivy"
  }
}

test {
  useJUnitPlatform()
}

ext {
  SCALA_VERSION = "2.13"
  PLAY_VERSION = "2.7.3"
  JUNIT_VERSION = "1.6.2"
  POSTGRES_URL = System.getenv("POSTGRES_URL") ?: "jdbc:postgresql://localhost:5432/alignment"
  POSTGRES_USER = System.getenv("POSTGRES_USER") ?: "postgres"
  POSTGRES_PASSWORD = System.getenv("POSTGRES_PASSWORD") ?: "postgres"
}

spotless {
  scala {
    target fileTree(rootDir) {
      include '**/*.scala'
      exclude '**/build'
    }
    scalafmt("2.5.2").configFile("$rootDir/.scalafmt.conf")
  }
}

play {
  platform {
    playVersion = PLAY_VERSION
    scalaVersion = SCALA_VERSION
    javaVersion = JavaVersion.VERSION_1_8
  }
  injectedRoutesGenerator = true
  jar {
    enabled = true
  }
}

flyway {
  url = POSTGRES_URL
  user = POSTGRES_USER
  password = POSTGRES_PASSWORD
  connectRetries = 10
  locations =  ['filesystem:app/flyway/src/main/resources/db/migration']
}

dependencies {
  // play
  implementation "com.typesafe.play:play-guice_${SCALA_VERSION}:${PLAY_VERSION}"
  implementation "ch.qos.logback:logback-classic:1.2.3"
  testImplementation "org.scalatestplus.play:scalatestplus-play_${SCALA_VERSION}:4.0.3"
  // test deps
  testImplementation "org.junit.platform:junit-platform-engine:${JUNIT_VERSION}"
  testImplementation "org.junit.platform:junit-platform-launcher:${JUNIT_VERSION}"
  testImplementation "co.helmethair:scalatest-junit-runner:0.1.6"
  // postgres
  implementation "org.postgresql:postgresql:42.2.12"
}
